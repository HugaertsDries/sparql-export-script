PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX lblodOrg: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX organisatie: <https://data.vlaanderen.be/ns/organisatie#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX loc: <http://www.w3.org/2007/uwa/context/location.owl#>
PREFIX regorg: <http://www.w3.org/ns/regorg#>
CONSTRUCT {
  ?s ?p ?o
}
WHERE {
  {
    SELECT DISTINCT (?site AS ?s)
    WHERE {
      VALUES ?eredienstType {
        erediensten:BestuurVanDeEredienst
        erediensten:CentraalBestuurVanDeEredienst
      }
      ?eredienst
        a ?eredienstType;
        skos:prefLabel ?prefLabelEredienst;
         org:hasPrimarySite ?site;
        # ACTIVE
        regorg:orgStatus <http://lblod.data.gift/concepts/63cc561de9188d64ba5840a42ae8f0d6>.
      ?site organisatie:bestaatUit ?address.
      ?bestuursorgaan besluit:bestuurt ?eredienst.
      ?bestuursorgaanInTijd generiek:isTijdspecialisatieVan ?bestuursorgaan.
      {
        ?bestuursorgaanInTijd mandaat:bindingStart ?bindingStart.
      }
      UNION
      {
        ?bestuursorgaanInTijd mandaat:bindingEinde ?bindingEinde.
      }
      FILTER (!REGEX(LCASE(?prefLabelEredienst), "^.*((\\byassin\\b)|(\\bdubbel\\b)|(\\barchiveren\\b)).*")).
    }
  }
  VALUES ?p {
    rdf:type
    mu:uuid
    organisatie:bestaatUit
  }
  ?s a org:Site.
  ?s ?p ?o.
}