PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX lblodOrg: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX organisatie: <https://data.vlaanderen.be/ns/organisatie#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX loc: <http://www.w3.org/2007/uwa/context/location.owl#>
PREFIX regorg: <http://www.w3.org/ns/regorg#>
CONSTRUCT { ?s ?p ?o }
WHERE {
  {
    SELECT DISTINCT (?eredienst AS ?s)
    WHERE {
      VALUES ?eredienstType {
        erediensten:BestuurVanDeEredienst
        erediensten:CentraalBestuurVanDeEredienst
      }
      ?eredienst
        a ?eredienstType;
        skos:prefLabel ?prefLabelEredienst;
        # has a org:Site
        org:hasPrimarySite ?site;
        # is ACTIVE
        regorg:orgStatus <http://lblod.data.gift/concepts/63cc561de9188d64ba5840a42ae8f0d6>.
      # org:Site has a locn:Address
      ?site organisatie:bestaatUit ?address.
      # has besluit:Bestuursgaan
      ?bestuursorgaan besluit:bestuurt ?eredienst.
      # has besluit:Bestuursgaan "in tijd" with either a start-or a start-& end-date
      ?bestuursorgaanInTijd generiek:isTijdspecialisatieVan ?bestuursorgaan.
      ?bestuursorgaanInTijd mandaat:bindingStart ?bindingStart.
      OPTIONAL {
        ?bestuursorgaanInTijd mandaat:bindingEinde ?bindingEinde.
      }
      # skos:prefLabel does not idicate wrong or double input
      FILTER (!REGEX(LCASE(?prefLabelEredienst), "^.*((\\byassin\\b)|(\\bdubbel\\b)|(\\barchiveren\\b)).*")).
    }
  }
  {
    VALUES ?p {
      rdf:type
      mu:uuid
      org:classification
      skos:prefLabel
      skos:altLabel
      org:hasPrimarySite
    }
    ?s ?p ?o.
  }
  UNION
  {
    VALUES ?p {
      adms:identifier
    }
    ?s ?p ?o.
    # FILTER
    ?o skos:notation "KBO nummer".
  }
}