PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX lblodOrg: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX organisatie: <https://data.vlaanderen.be/ns/organisatie#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX loc: <http://www.w3.org/2007/uwa/context/location.owl#>
PREFIX regorg: <http://www.w3.org/ns/regorg#>
CONSTRUCT {
  ?s ?p ?o
}
WHERE {
  {
    SELECT DISTINCT (?classificationBestuursorgaan AS ?s)
    WHERE {
      VALUES ?eredienstType {
        erediensten:BestuurVanDeEredienst
        erediensten:CentraalBestuurVanDeEredienst
      }
      ?eredienst
        a ?eredienstType;
        # ACTIVE
        regorg:orgStatus <http://lblod.data.gift/concepts/63cc561de9188d64ba5840a42ae8f0d6>.
      ?bestuursorgaan
        besluit:bestuurt ?eredienst;
        org:classification ?classificationBestuursorgaan.
    }
  }
  VALUES ?p {
    rdf:type
    mu:uuid
    skos:inScheme
    skos:topConceptOf
    skos:prefLabel
  }
  ?s a lblodOrg:BestuursorgaanClassificatieCode.
  ?s ?p ?o .
}